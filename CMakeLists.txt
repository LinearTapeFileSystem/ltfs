cmake_minimum_required(VERSION 3.26)
project(ltfs LANGUAGES C)
set(VERSION "2.5.0.0 (Prelim cmake)")
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/.cmake")

if(NOT CMAKE_BUILD_TYPE MATCHES "Debug|Release")
	set(CMAKE_BUILD_TYPE Debug)
endif()

# Add dependencies
include(GNUInstallDirs REQUIRED)
include(LTFSUtils REQUIRED)
find_package(Threads REQUIRED)
find_package(ICU COMPONENTS uc)
find_package(LibXml2 REQUIRED)
find_package(FUSE REQUIRED)
find_package(UUID REQUIRED)
find_package(NetSNMP)
# Configure project
set(DEFAULT_TAPE "" CACHE STRING "default tape backend plugin, e.g. sg, iokit, cam, scsipi-ibmtape")
set(DEFAULT_IOSCHED "unified" CACHE STRING "default I/O scheduler plugin, e.g. unified")
set(DEFAULT_KMI "none" CACHE STRING "default key manager interface plugin, e.g. none")
option(MSG_CHECK "compile in message checking mode (may be binary is defunct)" OFF)
option(POSIXLINK_ONLY "compile with livelink mode support" ON)
option(USE_NEW_LOCKING "use new locking system or not" ON)
option(ENABLE_LIN_TAPE "support IBM's lin_tape driver or not" OFF)
option(SUPPORT_BUGGY_IFS "support buggy I/Fs for tape drivers" OFF)
option(INDENT_INDEXES "enable xml indentation for index" OFF)
option(FORMAT_SPEC25 "support format spec 2.5 or not" OFF)
option(ICU_6X "force to use ICU6x (unorm2) functions" OFF)
option(HAVE_XML_PARSE_HUGE "enable xml parsing with huge file support" OFF)
option_tape_backend()
option_unorm2()
add_compile_definitions(_GNU_SOURCE __CMAKE_BUILD)

# Generate targets and configrations
add_subdirectory(messages)
add_subdirectory(src)
add_subdirectory(conf)
add_subdirectory(man)
configure_file(ltfs.pc.in ltfs.pc)
